@PACKAGE_INIT@



####################################################################
### Append search paths for find_package and find_library calls  ###
####################################################################

# Search the same directories as during configure
set(Eigen3_ROOT @PACKAGE_Eigen3_ROOT@)
set(spdlog_ROOT @PACKAGE_spdlog_ROOT@)
set(fmt_ROOT @PACKAGE_fmt_ROOT@)
set(HDF5_ROOT @PACKAGE_HDF5_ROOT@)

# Make sure we use h5pp find modules
list(INSERT CMAKE_MODULE_PATH 0 ${CMAKE_CURRENT_LIST_DIR})

# Append search paths for find_package and find_library calls
list(APPEND CMAKE_PREFIX_PATH ${PACKAGE_PREFIX_DIR})
if(EXISTS ${PACKAGE_PREFIX_DIR}/h5pp)
    list(INSERT CMAKE_PREFIX_PATH 0 ${PACKAGE_PREFIX_DIR}/h5pp)
    list(INSERT h5pp_ROOT 0 ${PACKAGE_PREFIX_DIR}/h5pp)
endif()


include(CMakeFindDependencyMacro)
if (NOT TARGET Eigen3::Eigen AND Eigen3::Eigen IN_LIST H5PP_ENABLED_TARGETS)
    find_dependency(Eigen3 3.3 REQUIRED)
    if(TARGET Eigen3 AND NOT TARGET Eigen3::Eigen)
        add_library(Eigen3::Eigen ALIAS Eigen3)
    endif()
endif()
if(TARGET Eigen3::Eigen)
    list(APPEND H5PP_FOUND_TARGETS Eigen3::Eigen)
endif()

if (NOT TARGET fmt::fmt AND fmt::fmt IN_LIST H5PP_ENABLED_TARGETS)
    find_dependency(fmt 6.2.1 REQUIRED)
    if(TARGET fmt AND NOT TARGET fmt::fmt)
        add_library(fmt::fmt ALIAS fmt)
    endif()
endif()
if(TARGET fmt::fmt)
    list(APPEND H5PP_FOUND_TARGETS fmt::fmt)
endif()

if (NOT TARGET spdlog::spdlog AND spdlog::spdlog IN_LIST H5PP_ENABLED_TARGETS)
    find_dependency(spdlog 1.3.1 REQUIRED)
    if(TARGET spdlog AND NOT TARGET spdlog::spdlog)
        add_library(spdlog::spdlog ALIAS spdlog)
    endif()
endif()
if(TARGET spdlog::spdlog)
    list(APPEND H5PP_FOUND_TARGETS spdlog::spdlog)
endif()

if (NOT TARGET hdf5::all AND hdf5::all IN_LIST H5PP_ENABLED_TARGETS)
    find_dependency(HDF5 1.8 COMPONENTS C HL REQUIRED)
endif()

if(TARGET hdf5::all)
    list(APPEND H5PP_FOUND_TARGETS  hdf5::all)
endif()

if(NOT H5PP_FIND_QUIETLY)
    message(STATUS "h5pp: Found dependency targets: ${H5PP_FOUND_TARGETS}")
endif()