cmake_minimum_required(VERSION 3.14)
cmake_policy(SET CMP0074 NEW)

if(SUPER_CACHEFILE_DIR)
    message(STATUS "Detected external cache file: ${SUPER_CACHEFILE_DIR}")
    load_cache(${SUPER_CACHEFILE_DIR} EXCLUDE CMAKE_INSTALL_PREFIX)
endif()

project(external-hdf5 C CXX)
option(HDF5_ENABLE_PARALLEL "Enables HDF5 Parallel MPI" OFF)
option(HDF5_ENABLE_SZIP_SUPPORT "Enable SZIP compression" OFF)
option(HDF5_ENABLE_Z_LIB_SUPPORT "Enable ZLIB compression" OFF)


if(ENV{CONDA_PREFIX})
    message(WARNING "The current conda environment may conflict with the build of HDF5: $ENV{CONDA_PREFIX}")
endif()

if(BUILD_SHARED_LIBS)
    set(CMAKE_FIND_LIBRARY_SUFFIXES ${CMAKE_SHARED_LIBRARY_SUFFIX})
else()
    set(CMAKE_FIND_LIBRARY_SUFFIXES ${CMAKE_STATIC_LIBRARY_SUFFIX})
endif()

if(NOT SZIP_LIBRARY)
    find_library(SZIP_LIBRARY NAMES sz HINTS ${CMAKE_INSTALL_PREFIX} ${CMAKE_INSTALL_PREFIX}/.. PATH_SUFFIXES aec aec/lib)
endif()
if(NOT AEC_LIBRARY)
    find_library(AEC_LIBRARY NAMES aec HINTS ${CMAKE_INSTALL_PREFIX} ${CMAKE_INSTALL_PREFIX}/.. PATH_SUFFIXES aec aec/lib)
endif()
if(NOT ZLIB_LIBRARY)
    find_library(ZLIB_LIBRARY NAMES  z HINTS ${CMAKE_INSTALL_PREFIX} ${CMAKE_INSTALL_PREFIX}/.. PATH_SUFFIXES zlib zlib/lib)
endif()

if(SZIP_LIBRARY)
    set(HDF5_ENABLE_SZIP_SUPPORT  ON CACHE BOOL "HDF5 will use SZIP library: ${SZIP_LIBRARY};${AEC_LIBRARY}" FORCE)
endif()
if(ZLIB_LIBRARY)
    set(HDF5_ENABLE_Z_LIB_SUPPORT ON CACHE BOOL "HDF5 will use ZLIB library: ${ZLIB_LIBRARY}" FORCE)
endif()

message(STATUS "HDF5 will use SZIP ${HDF5_ENABLE_SZIP_SUPPORT}: ${SZIP_LIBRARY}")
message(STATUS "HDF5 will use AEC  ${HDF5_ENABLE_SZIP_SUPPORT}: ${AEC_LIBRARY}")
message(STATUS "HDF5 will use ZLIB ${HDF5_ENABLE_Z_LIB_SUPPORT}: ${ZLIB_LIBRARY}")

include(ExternalProject)
ExternalProject_Add(external_hdf5
        URL         https://support.hdfgroup.org/ftp/HDF5/releases/hdf5-1.12/hdf5-1.12.0/src/hdf5-1.12.0.tar.gz
        URL_MD5     9e22217d22eb568e09f0cc15fb641d7c
        PREFIX      ${CMAKE_BINARY_DIR}
        INSTALL_DIR ${CMAKE_INSTALL_PREFIX}
        BUILD_ALWAYS TRUE
        CMAKE_ARGS
        -DCMAKE_POLICY_DEFAULT_CMP0074=NEW
        -DCMAKE_CXX_STANDARD=${CMAKE_CXX_STANDARD}
        -DCMAKE_CXX_STANDARD_REQUIRED:BOOL=${CMAKE_CXX_STANDARD_REQUIRED}
        -DCMAKE_CXX_EXTENSIONS:BOOL=${CMAKE_CXX_EXTENSIONS}
        -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR>
        -DCMAKE_INSTALL_MESSAGE=NEVER
        # HDF5 flags
        -DBUILD_TESTING:BOOL=OFF
        -DZLIB_LIBRARY=${ZLIB_LIBRARY}
        -DSZIP_LIBRARY=${SZIP_LIBRARY}$<SEMICOLON>${AEC_LIBRARY}
        -DHDF5_ENABLE_PARALLEL:BOOL=${HDF5_ENABLE_PARALLEL}
        -DHDF5_ENABLE_Z_LIB_SUPPORT:BOOL=${HDF5_ENABLE_Z_LIB_SUPPORT}
        -DHDF5_ENABLE_SZIP_SUPPORT:BOOL=${HDF5_ENABLE_SZIP_SUPPORT}
        -DHDF5_BUILD_TOOLS:BOOL=ON
        -DHDF5_BUILD_FORTRAN:BOOL=OFF
        -DHDF5_BUILD_EXAMPLES:BOOL=OFF
        -DHDF5_BUILD_JAVA:BOOL=OFF
        -DHDF5_DISABLE_COMPILER_WARNINGS:BOOL=ON
        -DCMAKE_BUILD_WITH_INSTALL_RPATH:BOOL=ON
        -DALLOW_UNSUPPORTED=ON
        )

