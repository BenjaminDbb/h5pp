cmake_minimum_required(VERSION 3.15)
function(pad_string OUT_VARIABLE DESIRED_LENGTH FILL_CHAR VALUE)
    string(LENGTH "${VALUE}" VALUE_LENGTH)
    math(EXPR REQUIRED_PADS "${DESIRED_LENGTH} - ${VALUE_LENGTH}")
    set(PAD ${VALUE})
    if(REQUIRED_PADS GREATER 0)
        math(EXPR REQUIRED_MINUS_ONE "${REQUIRED_PADS} - 1")
        foreach(FOO RANGE ${REQUIRED_MINUS_ONE})
            set(PAD "${PAD}${FILL_CHAR}")
        endforeach()
    endif()
    set(${OUT_VARIABLE} "${PAD}" PARENT_SCOPE)
endfunction()

function(remove_empty_genexpr list_data)
    foreach(elem ${${list_data}})
#            message(${elem})
        if(elem MATCHES "-|/")
            list(APPEND match_list ${elem})
        elseif(elem MATCHES "$<" OR elem MATCHES ">")
#            message("Discarding: ${elem}")
        else()
            list(APPEND match_list ${elem})
        endif()
    endforeach()
    set(${list_data} ${match_list} PARENT_SCOPE)
endfunction()

function(get_imported_location location target_name)
    get_target_property(PROP_IMP ${target_name} IMPORTED)
    get_target_property(PROP_TYP ${target_name} TYPE)
    if(PROP_IMP AND NOT PROP_TYP MATCHES "INTERFACE")
        string(TOUPPER "${CMAKE_BUILD_TYPE}" BUILD_TYPE)
        set(PROP_CFGS _${BUILD_TYPE};_${CMAKE_BUILD_TYPE};;)
        set(PROP_VARS IMPORTED_LOCATION;LOCATION)
        foreach(PROP_CFG ${PROP_CFGS})
            foreach(PROP_VAR ${PROP_VARS})
                set(PROP ${PROP_VAR}${PROP_CFG})
                get_property(PROP_SET TARGET "${target_name}" PROPERTY "${PROP}" SET)
                if(PROP_SET)
                    get_target_property(PROP_LOC ${target_name} "${PROP}")
                    if(${PROP_LOC} MATCHES "NOTFOUND")
                        unset(PROP_LOC)
                    else()
                        set(${location} "${PROP_LOC}" PARENT_SCOPE)
                        return()
                    endif()
                endif()
            endforeach()
        endforeach()
    endif()
endfunction()

function(expand_target_all_targets target_names expanded_list)
    foreach(target_name ${target_names})
        if(TARGET ${target_name} AND NOT ${target_name} IN_LIST expanded_list)
            list(APPEND target_names_expanded ${target_name})
            unset(interface_libs)
            unset(private_libs)
            unset(imported_lib)
            unset(lib_type)
            get_target_property(lib_type ${target_name} TYPE)
            get_imported_location(imported_lib ${target_name} )
            if(NOT lib_type MATCHES "INTERFACE")
                get_target_property(private_libs ${target_name} LINK_LIBRARIES)
            endif()
            get_target_property(interface_libs ${target_name} INTERFACE_LINK_LIBRARIES)
            list(FILTER imported_lib EXCLUDE REGEX "NOTFOUND|(-o)$")
            list(FILTER private_libs EXCLUDE REGEX "NOTFOUND|(-o)$")
            list(FILTER interface_libs EXCLUDE REGEX "NOTFOUND|(-o)$")
            foreach(elem ${imported_lib};${private_libs};${interface_libs})
                string(REGEX REPLACE "([\$<]+[A-Za-z]+:[A-Za-z]+[:>]+)|:>|>" "" elem_stripped "${elem}")
                if(NOT TARGET ${elem_stripped})
                    continue()
                endif()
                if(${elem_stripped} IN_LIST target_names)
                    continue()
                endif()
                if(${elem} IN_LIST expanded_list)
                    continue()
                endif()
                if(${elem_stripped} IN_LIST target_names_expanded)
                    continue()
                endif()
                unset(recursed_list) # Otherwise this one grows for each elem
                expand_target_all_targets(${elem_stripped} recursed_list)
                list(REMOVE_DUPLICATES "recursed_list")
                foreach(rec ${recursed_list})
                    if(NOT TARGET ${rec})
                        continue()
                    endif()
                    if(${rec} IN_LIST target_names)
                        continue()
                    endif()
                    if(${rec} IN_LIST expanded_list)
                        continue()
                    endif()
                    if(${rec} IN_LIST target_names_expanded)
                        continue()
                    endif()
                    list(APPEND target_names_expanded ${rec})
                endforeach()
            endforeach()
        endif()
    endforeach()

    # Remove duplicates in a way that retains linking order, i.e. keep last occurrence
    if(target_names_expanded)
        list(REVERSE target_names_expanded)
        list(REMOVE_DUPLICATES "target_names_expanded")
        list(REVERSE "target_names_expanded")
        list(APPEND ${target_names_expanded} "${${expanded_list}}")
    endif()
    list(APPEND ${expanded_list} "${target_names_expanded}")
    set(${expanded_list} "${${expanded_list}}" PARENT_SCOPE)
endfunction()


function(print_target_info target_name prefix)
    if(TARGET ${target_name})
        get_target_property(PROP_INC  ${target_name} INTERFACE_INCLUDE_DIRECTORIES)
        get_target_property(PROP_LIB  ${target_name} INTERFACE_LINK_LIBRARIES)
        get_target_property(PROP_OPT  ${target_name} INTERFACE_COMPILE_OPTIONS)
        get_target_property(PROP_DEF  ${target_name} INTERFACE_COMPILE_DEFINITIONS)
        get_target_property(PROP_FTR  ${target_name} INTERFACE_COMPILE_FEATURES)
        get_target_property(PROP_TYP  ${target_name} TYPE)
        get_target_property(PROP_IMP  ${target_name} IMPORTED)
        get_imported_location(PROP_LOC ${target_name})

        remove_empty_genexpr(PROP_INC)
        remove_empty_genexpr(PROP_LIB)
        remove_empty_genexpr(PROP_OPT)
        remove_empty_genexpr(PROP_DEF)
        remove_empty_genexpr(PROP_FTR)

        pad_string(padded_target "64" " " "${prefix}[${target_name}]" )
        if(PROP_LOC)
            message(VERBOSE "${padded_target} IMPORTS : ${PROP_LOC}" )
        endif()
        if(PROP_LIB)
            list(REMOVE_DUPLICATES PROP_LIB)
            message(VERBOSE "${padded_target} INTRFCE : ${PROP_LIB}" )
        endif()
        if(PROP_INC)
            list(REMOVE_DUPLICATES PROP_INC)
            message(VERBOSE "${padded_target} INCLUDE : ${PROP_INC}" )
        endif()
        if(PROP_OPT)
            list(REMOVE_DUPLICATES PROP_OPT)
            message(VERBOSE "${padded_target} OPTIONS : ${PROP_OPT}" )
        endif()
        if(PROP_DEF)
            list(REMOVE_DUPLICATES PROP_DEF)
            message(VERBOSE "${padded_target} DEFINES : ${PROP_DEF}" )
        endif()
        if(PROP_FTR)
            message(VERBOSE "${padded_target} FEATURE : ${PROP_FTR}" )
        endif()
    endif()
endfunction()


function(print_compiler_info prefix)
    pad_string(padded_string "64" " " "${prefix}[${CMAKE_CXX_COMPILER_ID} ${CMAKE_CXX_COMPILER_VERSION}]" )
    if(CMAKE_BUILD_TYPE STREQUAL "Release")
        message(VERBOSE "${padded_string} OPTIONS : ${CMAKE_CXX_FLAGS} ${CMAKE_CXX_FLAGS_RELEASE}" )
    endif()
    if(CMAKE_BUILD_TYPE STREQUAL "Debug")
        message(VERBOSE "${padded_string} OPTIONS : ${CMAKE_CXX_FLAGS} ${CMAKE_CXX_FLAGS_DEBUG}" )
    endif()
    if(CMAKE_BUILD_TYPE STREQUAL "RelWithDebInfo")
        message(VERBOSE "${padded_string} OPTIONS : ${CMAKE_CXX_FLAGS} ${CMAKE_CXX_FLAGS_RELWITHDEBINFO}" )
    endif()
    if(CMAKE_BUILD_TYPE STREQUAL "MinSizeRel")
        message(VERBOSE "${padded_string} OPTIONS : ${CMAKE_CXX_FLAGS} ${CMAKE_CXX_FLAGS_MINSIZEREL}" )
    endif()
endfunction()

# Print summary of project targets
function(print_target_summary prj)
    string(TOUPPER ${prj} PRJ)
    if(NOT TARGET ${prj})
        message(WARNING "print_target_summary: ${prj} is not a valid target")
        return()
    endif()
    message(VERBOSE "| ${PRJ} TARGET SUMMARY")
    message(VERBOSE "|--------------------------")
    expand_target_all_targets(${prj} TARGET_EXPANDED)
    foreach (t ${TARGET_EXPANDED})
        print_target_info(${t} "| ")
    endforeach ()
    message(VERBOSE "|--------------------------")
endfunction()